#created on: 14.11.2007
package org.drools.verifier.incoherence

#list any import classes here.
import org.drools.verifier.components.VerifierRule;
import org.drools.verifier.components.LiteralRestriction;
import org.drools.verifier.components.SubPattern;
import org.drools.verifier.components.SubRule;
import org.drools.verifier.components.Pattern;
import org.drools.verifier.components.VariableRestriction;
import org.drools.verifier.report.components.Cause;
import org.drools.verifier.report.components.MissingRange;
import org.drools.verifier.report.components.VerifierMessage;
import org.drools.verifier.report.components.Severity;
import org.drools.verifier.report.components.MessageType;

import org.drools.verifier.data.VerifierReport;

import java.util.Collection;
import java.util.ArrayList;

import org.drools.base.evaluators.Operator;

#declare any global variables here
global VerifierReport result;

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncorencePattern( a == 1 )
#		not IncorencePattern( a == 1 )
#
rule "Incoherent Patterns in rule possibility"
	when
		$r1 :LiteralRestriction(
			patternIsNot == true
		)
	
		$r2 :LiteralRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			restrictionType == $r1.restrictionType,
			fieldGuid matches $r1.fieldGuid,
			valueType == $r1.valueType,
			operator == $r1.operator
		)

		eval( $r1.compareValues( $r2 ) == 0 )

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
	then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		$var :Object()
#		IncorencePattern( a == $var )
#		not IncorencePattern( a == $var )
#
rule "Incoherent Patterns in rule possibility, variables"
	when
		$r1 :VariableRestriction(
			patternIsNot == true
		)

		$r2 :VariableRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			fieldGuid matches $r1.fieldGuid,
			variable.objectTypeGuid matches $r1.variable.objectTypeGuid,
			variable.objectTypeType == $r1.variable.objectTypeType,
			operator == $r1.operator
		)

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)


		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
	then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncorencePattern8( a > 11 )
#		not IncorencePattern8( a > 1 )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with lesser value"
	when
		$r1 :LiteralRestriction(
			patternIsNot == true,
			( operator == Operator.GREATER || == Operator.GREATER_OR_EQUAL )
		)

		$r2 :LiteralRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			( operator == Operator.GREATER || == Operator.GREATER_OR_EQUAL || == Operator.EQUAL ),
			fieldGuid matches $r1.fieldGuid
		)

		eval( $r1.compareValues( $r2 ) == -1 )

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
	then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncorencePattern( a < 1 )
#		not IncorencePattern( a < 11 )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with greater value"
	when
		$r1 :LiteralRestriction(
			patternIsNot == true,
			( operator == Operator.LESS || == Operator.LESS_OR_EQUAL )
		)

		$r2 :LiteralRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			( operator == Operator.LESS || == Operator.LESS_OR_EQUAL || == Operator.EQUAL ),
			fieldGuid matches $r1.fieldGuid
		)

		eval( $r1.compareValues( $r2 ) == 1 )

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
	then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncoherencePattern( a >= 1 )
#		not IncoherencePattern( a != 1 )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with equal or unequal value"
	when
		$r1 :LiteralRestriction(
			patternIsNot == true,
			operator == Operator.NOT_EQUAL
		)

		$r2 :LiteralRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			# It is also a problem if the value is NOT_EQUAL, but there is already a rule for that.
			( operator != Operator.EQUAL && != Operator.NOT_EQUAL ),
			fieldGuid matches $r1.fieldGuid
		)

		eval( $r1.compareValues( $r2 ) == 0 )

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncoherencePattern15( a >= $var )
#		not IncoherencePattern15( a != $var )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with equal or unequal variables"
	when
		$r1 :VariableRestriction(
			patternIsNot == true,
			operator == Operator.NOT_EQUAL
		)

		$r2 :VariableRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			fieldGuid matches $r1.fieldGuid,
			variable.objectTypeGuid matches $r1.variable.objectTypeGuid,
			variable.objectTypeType == $r1.variable.objectTypeType,
			# It is also a problem if the value is NOT_EQUAL, but there is already a rule for that.
			( operator != Operator.EQUAL && != Operator.NOT_EQUAL )
		)

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncoherencePattern13( a == $var )
#		not IncoherencePattern13( a >= $var )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with equal value"
	when
		$r1 :LiteralRestriction(
			patternIsNot == true,
			( operator == Operator.LESS_OR_EQUAL || == Operator.GREATER_OR_EQUAL )
		)

		$r2 :LiteralRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			operator == Operator.EQUAL,
			fieldGuid matches $r1.fieldGuid
		)

		eval( $r1.compareValues( $r2 ) == 0 )

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end

#
# If two Patterns are in conflict.
#
# Type: Warning
# Dependencies: None
# Example:
#		IncoherencePattern13( a == $var )
#		not IncoherencePattern13( a >= $var )
#
rule "Incoherent Patterns in rule possibility, ranges when not conflicts with equal variable"
	when
		$r1 :VariableRestriction(
			patternIsNot == true,
			# Equal is also a problem, but there is already a rule for that.
			( operator == Operator.LESS_OR_EQUAL || == Operator.GREATER_OR_EQUAL )
		)

		$r2 :VariableRestriction(
			ruleGuid matches $r1.ruleGuid,
			patternIsNot == false,
			fieldGuid matches $r1.fieldGuid,
			variable.objectTypeGuid == $r1.variable.objectTypeGuid,
			variable.objectTypeType == $r1.variable.objectTypeType,
			operator == Operator.EQUAL
		)

		$pp1 :SubPattern(
			patternGuid matches $r1.patternGuid
		)

		$pp2 :SubPattern(
			patternGuid matches $r2.patternGuid
		)

		# There is a problem if both of these are in the same SubRule.
		$rp :SubRule(
			 items contains $pp1,
			 items contains $pp2
		)

		$p1 :Pattern(
			guid matches $r1.patternGuid
		)

		$p2 :Pattern(
			guid matches $r2.patternGuid
		)

		$r :VerifierRule(
			guid matches $rp.ruleGuid
		)
then
		Collection list = new ArrayList();
		list.add( $p1 );
		list.add( $p2 );

		result.add( new VerifierMessage(
								Severity.WARNING,
								MessageType.INCOHERENCE,
								$r,
								"Pattern " + $p1 + " and " + $p2 +
								" are in conflict. Because of this, it is possible that the rule that contains them can never be satisfied.",
								list
								) );
end