#created on: 12.10.2007
package org.drools.verifier.redundancy

#list any import classes here.
global VerifierReport result;



#declare any global variables here
import java.util.Collection;
import java.util.ArrayList;

import org.drools.verifier.components.SubPattern;
import org.drools.verifier.components.SubRule;
import org.drools.verifier.data.VerifierReport;
import org.drools.verifier.report.components.Redundancy;
import org.drools.verifier.report.components.VerifierMessage;
import org.drools.verifier.report.components.Cause;
import org.drools.verifier.report.components.Severity;
import org.drools.verifier.report.components.MessageType;

import java.util.Map;
import java.util.HashMap;


#
# Dependencies:
#
#	Needs at least one rule from following rules:
#		package org.drools.verifier.redundancy.restrictions
#			rule "Find redundant VariableRestriction"
#			rule "Find redundant LiteralRestriction"
#
rule "Find redundant restrictions from pattern possibilities"
	salience -100
	when
		$r :Redundancy()

		$pp :SubPattern(
			items contains $r.items[0],
			items contains $r.items[1]
		)
	then
		Map<String,String> impactedRules = new HashMap<String,String>();
		impactedRules.put( $pp.getRuleGuid(), $pp.getRuleName());
		
		Collection causes = new ArrayList();
		causes.addAll( $r.getItems() );

		result.add(new VerifierMessage(
						impactedRules,
						Severity.NOTE, 
						MessageType.REDUNDANCY,
						$r, 
						"Same restriction exists twice.", 
						causes ) );
end

#
# Dependencies:
#
#		package org.drools.verifier.redundancy.possibilities
#			rule "Find pattern possibility redundancy"
#
rule "Find redundant pattern possibilities from rule possibilities"
	salience -100
	when
		$r :Redundancy()

		$sr :SubRule(
			items contains $r.items[0],
			items contains $r.items[1]
		)
	then
		Map<String,String> impactedRules = new HashMap<String,String>();
		impactedRules.put( $sr.getRuleGuid(), $sr.getRuleName());
		
		Collection causes = new ArrayList();
		causes.addAll( $r.getItems() );

		result.add(new VerifierMessage(
						impactedRules,
						Severity.NOTE, 
						MessageType.REDUNDANCY,
						$r, 
						"Two pattern possibilities with redundant restrictions exists twice in the same rule.", 
						causes ) );
end

