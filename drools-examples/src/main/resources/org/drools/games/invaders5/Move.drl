/*
 * Copyright 2015 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package org.drools.games.invaders5;

import org.drools.games.KeyPressed
import org.drools.games.Run

import org.drools.games.invaders.Ship
import org.drools.games.invaders.Unit
import org.drools.games.invaders.Invader
import org.drools.games.GameUI
import org.drools.games.invaders.InvadersConfiguration

global GameUI ui
global InvadersConfiguration conf

dialect "mvel"

rule ShipDeltaMoveLeft agenda-group "Move" when
    s : Ship()
        KeyPressed( keyText == "Z" )
then
    modify( s ) { dx = 0 - s.speed }
end

rule ShipDeltaStopLeft agenda-group "Move" when
    s : Ship()
        not KeyPressed( keyText == "Z" )
then
    modify( s ) { dx = 0 }
end


rule ShipDeltaMoveRight agenda-group "Move" when
    s : Ship()
        KeyPressed( keyText == "X" )
then
    modify( s ) { dx = s.speed }
end

rule ShipDeltaStopRight agenda-group "Move" when
    s : Ship()
        not KeyPressed( keyText == "X" )
then
    modify( s ) { dx = 0 }
end

rule ShipMove agenda-group "Move" when
    s : Ship( dx != 0, x + dx > 0,  x + dx + width < conf.windowWidth ) @watch( !x )
    Run()
then
    modify( s ) { x = s.x + s.dx }
end



