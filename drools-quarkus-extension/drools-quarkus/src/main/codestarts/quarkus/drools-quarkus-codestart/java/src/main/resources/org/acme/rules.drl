/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
 
package org.acme;

import org.acme.Applicant;
import org.acme.LoanApplication;

global Integer maxAmount;
global java.util.List approvedApplications;

rule SmallDepositApprove when
    $l: LoanApplication( applicant.age >= 20, deposit < 1000, amount <= 2000 )
then
    modify($l) { setApproved(true) };
end

rule SmallDepositReject when
    $l: LoanApplication( applicant.age >= 20, deposit < 1000, amount > 2000 )
then
    modify($l) { setApproved(false) };
end

rule LargeDepositApprove when
    $l: LoanApplication( applicant.age >= 20, deposit >= 1000, amount <= maxAmount )
then
    modify($l) { setApproved(true) };
end

rule LargeDepositReject when
    $l: LoanApplication( applicant.age >= 20, deposit >= 1000, amount > maxAmount )
then
    modify($l) { setApproved(false) };
end

rule NotAdultApplication when
    $l: LoanApplication( applicant.age < 20 )
then
    modify($l) { setApproved(false) };
end

rule CollectApprovedApplication when
    $l: LoanApplication( approved )
then
    approvedApplications.add($l);
end
