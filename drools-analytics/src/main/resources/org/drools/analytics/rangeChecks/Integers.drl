#created on: 7.6.2007
package org.drools.analytics.rangeChecks.integers;

#list any import classes here.
import org.drools.analytics.components.*;
import org.drools.analytics.dao.AnalyticsResult;
import org.drools.analytics.report.components.Gap;

import org.drools.analytics.dao.AnalyticsResult;

global AnalyticsResult result;

# If all ranges are not checked for a field.
# 
# Type: Warning
# Example: in "Rule 1" Foo( bar > 42 ) 
#						and in Foo( <= 42 || == 42 ) is missing.
#
rule "Range check for integers, if smaller than or equal is missing"
	when
		$f :Field( fieldType == Field.FieldType.INT )
		
		# Foo( bar > 42 )
		$r :LiteralRestriction( 
			fieldId == $f.id, 
			evaluator == ">"
		)
		
		# Check if Foo( bar == 42 || <= 42 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator  == "==" || == "<=" ), 
			patternIsNot == $r.patternIsNot, 
			intValue == $r.intValue 
		)
	then
		Gap gap = new Gap( $f, "<=", $r );
		
		gap.setFiredRuleName( drools.getRule().getName() );
		
		result.save( gap );
		insert( gap );
end

# If all ranges are not checked for a field.
# 
# Type: Warning
# Example: in "Rule 1" Foo( bar < 42 ) 
#						and in Foo( >= 42 || == 42 ) is missing.
#
rule "Range check for integers, if greater than or equal is missing"
	when
		$f :Field( fieldType == Field.FieldType.INT )
		
		# Foo( bar > 42 )
		$r :LiteralRestriction( 
			fieldId == $f.id, 
			evaluator == "<"
		)
		
		# Check if Foo( bar == 42 || <= 42 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator  == "==" || == ">=" ), 
			patternIsNot == $r.patternIsNot, 
			intValue == $r.intValue 
		)
	then
		Gap gap = new Gap( $f, ">=", $r );
		
		gap.setFiredRuleName( drools.getRule().getName() );
		
		result.save( gap );
		insert( gap );
end

# If all ranges are not checked for a field.
#
# Type: Warning
# Example: in "Rule 1" Foo( bar == 42 || bar <= 42 ) 
# 					and no Foo( bar > 42 || >= 42 || >= 43 || == 43 ) 
#						then Foo( bar > 42 ) is missing.
#
rule "Range check for integers, equal and greater than"
	when
		$f :Field( fieldType == Field.FieldType.INT )

		# Foo( bar == 42 ) 
		$r :LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == "==" || == "<=" )
		)

		# Check if Foo( bar > 42 || >= 42 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == ">" || == ">=" ), 
			patternIsNot == $r.patternIsNot, 
			intValue == $r.intValue 
		)
		
		# Check if Foo( bar == 43 || >= 43 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == "==" || == ">=" ), 
			patternIsNot == $r.patternIsNot, 
			eval( intValue == $r.getIntValue() + 1 ) 
		)
	then
		Gap gap =  new Gap( $f, ">", $r );
		
		gap.setFiredRuleName( drools.getRule().getName() );
		
		result.save( gap );
		insert( gap );
end

# If all ranges are not checked for a field.
#
# Type: Warning
# Example: in "Rule 1" Foo( bar == 42 || bar >= 42 ) 
# 					and no Foo( bar < 42 || <= 42 || <= 41 || == 41 ) 
#						then Foo( bar < 42 ) is missing.
#
rule "Range check for integers, equal and smaller than"
	when
		$f :Field( fieldType == Field.FieldType.INT )

		# Foo( bar == 42 ) 
		$r :LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == "==" || == ">=" )
		)

		# Check if Foo( bar < 42 || <= 42 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == "<" || == "<=" ), 
			patternIsNot == $r.patternIsNot, 
			intValue == $r.intValue 
		)
		
		# Check if Foo( bar == 41 || <= 41 ) is missing.
		not	LiteralRestriction( 
			fieldId == $f.id, 
			( evaluator == "==" || == "<=" ), 
			patternIsNot == $r.patternIsNot, 
			eval( intValue == $r.getIntValue() - 1 ) 
		)
	then
		Gap gap =  new Gap( $f, "<", $r );
		
		gap.setFiredRuleName( drools.getRule().getName() );
		
		result.save( gap );
		insert( gap );
end
