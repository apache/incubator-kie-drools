/*
 * Copyright 2017 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package org.kie.dmn.validation;

import org.kie.dmn.model.v1_1.*;
import org.kie.dmn.api.core.DMNMessage;
import org.kie.dmn.core.impl.DMNMessageImpl;
import org.kie.dmn.core.util.MsgUtil;
import org.kie.dmn.feel.lang.types.BuiltInType;
import org.kie.dmn.feel.parser.feel11.FEELParser;
import org.kie.dmn.feel.runtime.events.SyntaxErrorEvent;
import org.kie.dmn.api.feel.runtime.events.FEELEvent;
import org.kie.dmn.core.util.Msg;

import function org.kie.dmn.validation.ValidatorUtil.rightOfHash;

global org.kie.dmn.validation.MessageReporter reporter;

rule DECISION_MISSING_EXPR
when
    $d : Decision(expression == null)
then
    reporter.report(DMNMessage.Severity.ERROR, $d, Msg.MISSING_EXPRESSION_FOR_DECISION, $d.getIdentifierString());
end

rule DECISION_MISSING_VAR
when
    $d : Decision(variable == null)
then
    reporter.report(DMNMessage.Severity.ERROR, $d ,Msg.MISSING_VARIABLE_FOR_DECISION, $d.getIdentifierString());
end

rule DECISION_VAR_MISMATCH
when
    $d : Decision(variable != null, name != variable.name)
then
    reporter.report(DMNMessage.Severity.ERROR, $d, Msg.VARIABLE_NAME_MISMATCH_FOR_DECISION, $d.getVariable().getName(), $d.getName());
end

rule DECISION_PERF_INDICATOR_WRONG_TYPE
when
    $d: Decision($elemRef: /impactedPerformanceIndicator)
    $element: DMNElement(id == rightOfHash($elemRef.href), !(this instanceof PerformanceIndicator))
then
    reporter.report(DMNMessage.Severity.ERROR, $elemRef , Msg.REQ_DEP_INVALID_TYPE, $element.getId(), $d.getIdentifierString());
end

rule DECISION_DECISION_MAKER_WRONG_TYPE
when
    $d: Decision($elemRef: /decisionMaker)
    $element: DMNElement(id == rightOfHash($elemRef.href), !(this instanceof OrganizationUnit))
then
    reporter.report(DMNMessage.Severity.ERROR, $elemRef , Msg.REQ_DEP_INVALID_TYPE, $element.getId(), $d.getIdentifierString());
end

rule DECISION_DECISION_OWNER_WRONG_TYPE
when
    $d: Decision($elemRef: /decisionOwner)
    $element: DMNElement(id == rightOfHash($elemRef.href), !(this instanceof OrganizationUnit))
then
    reporter.report(DMNMessage.Severity.ERROR, $elemRef , Msg.REQ_DEP_INVALID_TYPE, $element.getId(), $d.getIdentifierString());
end