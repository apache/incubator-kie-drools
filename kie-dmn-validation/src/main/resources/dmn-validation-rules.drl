/*
 * Copyright 2016 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package org.kie.dmn.validation;

import org.kie.dmn.model.v1_1.*;
import org.kie.dmn.api.core.DMNMessage;
import org.kie.dmn.core.impl.DMNMessageImpl;
import org.kie.dmn.core.util.MsgUtil;
import org.kie.dmn.feel.lang.types.BuiltInType;
import org.kie.dmn.feel.parser.feel11.FEELParser;
import org.kie.dmn.feel.runtime.events.SyntaxErrorEvent;
import org.kie.dmn.api.feel.runtime.events.FEELEvent;

global org.kie.dmn.validation.MessageReporter reporter;

rule BKM_MISSING_EXPR
when
    $d : BusinessKnowledgeModel( encapsulatedLogic == null || encapsulatedLogic.expression == null )
then
    reporter.report( DMNMessage.Severity.ERROR, $d, Msg.BKM_MISSING_EXPR );
end

rule BKM_MISSING_VAR
when
    $d : BusinessKnowledgeModel()
    not( BusinessKnowledgeModel( /variable{name == $d.name}, this == $d ) )
then
    reporter.report( DMNMessage.Severity.ERROR,  $d , Msg.BKM_MISSING_VAR );
end

rule CONTEXT_DUP_ENTRY
when
  $ce1 : ContextEntry( parent instanceof Context )
  $ce2 : ContextEntry( variable!.name == $ce1.variable!.name, parent == $ce1.parent, this != $ce1 )
then
  reporter.report( DMNMessage.Severity.ERROR,  $ce1 , Msg.CONTEXT_DUP_ENTRY );
end

rule CONTEXT_ENTRY_NOTYPEREF
when
  $ce1 : ContextEntry( variable != null, variable.typeRef == null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $ce1 , Msg.CONTEXT_ENTRY_NOTYPEREF );
end

rule DRGELEM_NOT_UNIQUE
when
  $x : DRGElement()
  $y : DRGElement( name == $x.name, this != $x )
then
  reporter.report( DMNMessage.Severity.ERROR,  $x , Msg.DRGELEM_NOT_UNIQUE );
end

rule DECISION_MISSING_EXPR
when
    $d : Decision( expression == null )
then
    reporter.report( DMNMessage.Severity.ERROR,  $d , Msg.DECISION_MISSING_EXPR );
end

rule DECISION_MISSING_VAR
when
    $d : Decision()
    not( Decision( /variable{name == $d.name}, this == $d ) )
then
    reporter.report( DMNMessage.Severity.ERROR,  $d , Msg.DECISION_MISSING_VAR );
end

rule DTABLE_MULTIPLEOUT_NAME
when
  $oc : OutputClause( parent instanceof DecisionTable, parent.output.size > 1, this.name == null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.DTABLE_MULTIPLEOUT_NAME );
end

rule DTABLE_MULTIPLEOUT_TYPEREF
when
  $oc : OutputClause( parent instanceof DecisionTable, parent.output.size > 1, this.typeRef == null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.DTABLE_MULTIPLEOUT_TYPEREF );
end

rule DTABLE_PRIORITY_MISSING_OUTVALS
when
  $oc : OutputClause( parent instanceof DecisionTable, parent#DecisionTable.hitPolicy == HitPolicy.PRIORITY, outputValues == null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.DTABLE_PRIORITY_MISSING_OUTVALS );
end

rule DTABLE_SINGLEOUT_NONAME
when
  $oc : OutputClause( parent instanceof DecisionTable, parent.output.size == 1, this.name != null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.DTABLE_SINGLEOUT_NONAME );
end

rule DTABLE_SINGLEOUT_TYPEREF
when
  $oc : OutputClause( parent instanceof DecisionTable, parent.output.size == 1, this.typeRef != null )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.DTABLE_SINGLEOUT_NOTYPEREF );
end

rule ELEMREF_NOHASH_p1
when
  $oc : DMNElementReference( !href.startsWith("#"), !( href contains ":" ) ) 
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.ELEMREF_NOHASH );
end

rule ELEMREF_NOHASH_p2
when
  $oc : DMNElementReference( href contains ":", !href.substring(href.indexOf(":")+1).startsWith('#') )
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.ELEMREF_NOHASH );
end

function String rightOfHash(String input) {
    return input.substring(input.indexOf("#")+1);
}

rule ELEMREF_MISSING_TARGET
when
  $oc : DMNElementReference( $targetId : rightOfHash( href ) )
  not ( DMNElement( id == $targetId ) ) 
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.ELEMREF_MISSING_TARGET );
end

rule FORMAL_PARAM_DUPLICATED
when
    $fp1 : InformationItem( parent instanceof FunctionDefinition )
    $fp2 : InformationItem( name == $fp1.name, parent == $fp1.parent, this != $fp1 )
then
    reporter.report( DMNMessage.Severity.ERROR,  $fp1 , Msg.FORMAL_PARAM_DUPLICATED );
end

rule INPUTDATA_MISSING_VAR
when
    $id : InputData()
    not( InputData( /variable{name == $id.name}, this == $id ) )
then
    reporter.report( DMNMessage.Severity.ERROR,  $id , Msg.INPUTDATA_MISSING_VAR );
end

// The parameter names in the Binding elements SHALL be a subset of the formalParameters of the calledFunction.
rule INVOCATION_INCONSISTENT_PARAM_NAMES
when
    $inv : Invocation( $targetName : expression#LiteralExpression.text )
    $target : BusinessKnowledgeModel( name == $targetName )
    accumulate ( Binding($s : parameter.name) from $inv.binding ; 
                 $names : collectSet($s)  ) 
    accumulate ( InformationItem($ts : name) from $target.encapsulatedLogic.formalParameter ; 
                 $targetNames : collectSet($ts) ;
                 ! $targetNames.containsAll($names) ) 
then
    reporter.report( DMNMessage.Severity.ERROR,  $inv , Msg.INVOCATION_INCONSISTENT_PARAM_NAMES );
end

rule INVOCATION_MISSING_TARGET
when
    $inv : Invocation( $targetName : expression#LiteralExpression.text )
    not( DRGElement( name == $targetName ) )  
then
    reporter.report( DMNMessage.Severity.ERROR,  $inv , Msg.INVOCATION_MISSING_TARGET );
end

rule INVOCATION_WRONG_PARAM_COUNT
when
    $inv : Invocation( $targetName : expression#LiteralExpression.text )
    BusinessKnowledgeModel( name == $targetName, encapsulatedLogic.formalParameter.size != $inv.binding.size ) 
then
    reporter.report( DMNMessage.Severity.ERROR,  $inv , Msg.INVOCATION_WRONG_PARAM_COUNT );
end

// This is a lower level complement of ITEMDEF_NOT_UNIQUE below, for detecting duplicate attribute names inside the same ItemDefinition 
rule ITEMCOMP_DUPLICATED
when
  $ic1 : ItemDefinition( parent instanceof ItemDefinition )
  $ic2 : ItemDefinition( name == $ic1.name, parent == $ic1.parent, this != $ic1 )
then
  reporter.report( DMNMessage.Severity.ERROR,  $ic1 , Msg.ITEMCOMP_DUPLICATED );
end

rule ITEMDEF_NOT_UNIQUE
when
  $x : ItemDefinition( !(parent instanceof ItemDefinition) )
  $y : ItemDefinition( !(parent instanceof ItemDefinition), name == $x.name, this != $x )
then
  reporter.report( DMNMessage.Severity.ERROR,  $x , Msg.ITEMDEF_NOT_UNIQUE );
end

rule NAME_INVALID
when
    $ne : NamedElement( parent != null, !(parent instanceof DRGElement),                       // << functional optimization to avoid double reporting as Decision name == variable name 
                        FEELParser.isVariableNameValid( name ) == false )
then
//    java.util.List<FEELEvent> see = FEELParser.checkVariableName( $ne.getName() );
//    String customMessage = see.isEmpty()? Msg.NAME_INVALID : see.get(0).getMessage()
    reporter.report( DMNMessage.Severity.ERROR,  $ne , Msg.NAME_INVALID );
end

rule RELATION_DUP_COLUMN
when
  $c1 : InformationItem( parent instanceof Relation )
  $c2 : InformationItem( name == $c1.name, parent == $c1.parent, this != $c1 )
then
  reporter.report( DMNMessage.Severity.ERROR,  $c1 , Msg.RELATION_DUP_COLUMN );
end

rule RELATION_ROW_CELL_NOTLITERAL
when
  $e : Expression( parent instanceof org.kie.dmn.model.v1_1.List, parent.parent instanceof Relation, ! (this instanceof LiteralExpression) )
then
  reporter.report( DMNMessage.Severity.ERROR,  $e , Msg.RELATION_ROW_CELL_NOTLITERAL );
end

rule RELATION_ROW_CELLCOUNTMISMATCH
when
  $row : org.kie.dmn.model.v1_1.List( parent instanceof Relation, expression.size != parent.column.size )
then
  reporter.report( DMNMessage.Severity.ERROR,  $row , Msg.RELATION_ROW_CELLCOUNTMISMATCH );
end

rule REQAUTH_NOT_KNOWLEDGESOURCE
when
  AuthorityRequirement( requiredAuthority != null, $oc: requiredAuthority )
  DMNElement( id == rightOfHash( $oc.href ) , !(this instanceof KnowledgeSource) ) 
then
  reporter.report( DMNMessage.Severity.ERROR,  $oc , Msg.REQAUTH_NOT_KNOWLEDGESOURCE );
end

rule TYPEREF_NO_FEEL_TYPE_p1
when
  $o: Expression( typeRef!.prefix != null, getNamespaceURI( typeRef.prefix ) == DMNModelInstrumentedBase.URI_FEEL, BuiltInType.determineTypeFromName( typeRef.localPart ) == BuiltInType.UNKNOWN )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NO_FEEL_TYPE );
end

rule TYPEREF_NO_FEEL_TYPE_p2
when
  $o: InformationItem( typeRef!.prefix != null, getNamespaceURI( typeRef.prefix ) == DMNModelInstrumentedBase.URI_FEEL, BuiltInType.determineTypeFromName( typeRef.localPart ) == BuiltInType.UNKNOWN )
then
  $o: reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NO_FEEL_TYPE );
end

rule TYPEREF_NO_FEEL_TYPE_p3
when
  $o: ItemDefinition( typeRef!.prefix != null, getNamespaceURI( typeRef.prefix ) == DMNModelInstrumentedBase.URI_FEEL, BuiltInType.determineTypeFromName( typeRef.localPart ) == BuiltInType.UNKNOWN )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NO_FEEL_TYPE );
end

rule TYPEREF_NO_FEEL_TYPE_p4
when
  $o: OutputClause( typeRef!.prefix != null, getNamespaceURI( typeRef.prefix ) == DMNModelInstrumentedBase.URI_FEEL, BuiltInType.determineTypeFromName( typeRef.localPart ) == BuiltInType.UNKNOWN )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NO_FEEL_TYPE );
end

rule TYPEREF_NOT_FEEL_NOT_DEF_p1
when
  $o: Expression( typeRef != null, typeRef.prefix == null || getNamespaceURI( typeRef.prefix ) != DMNModelInstrumentedBase.URI_FEEL, $typeRef : typeRef )
  not( ItemDefinition( name == $typeRef ) )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NOT_FEEL_NOT_DEF );
end

rule TYPEREF_NOT_FEEL_NOT_DEF_p2
when
  $o: InformationItem( typeRef != null, typeRef.prefix == null || getNamespaceURI( typeRef.prefix ) != DMNModelInstrumentedBase.URI_FEEL, $typeRef : typeRef )
  not( ItemDefinition( name == $typeRef ) )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NOT_FEEL_NOT_DEF );
end

rule TYPEREF_NOT_FEEL_NOT_DEF_p3
when
  $o: ItemDefinition( typeRef != null, typeRef.prefix == null || getNamespaceURI( typeRef.prefix ) != DMNModelInstrumentedBase.URI_FEEL, $typeRef : typeRef )
  not( ItemDefinition( name == $typeRef ) )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NOT_FEEL_NOT_DEF );
end

rule TYPEREF_NOT_FEEL_NOT_DEF_p4
when
  $o: OutputClause( typeRef != null, typeRef.prefix == null || getNamespaceURI( typeRef.prefix ) != DMNModelInstrumentedBase.URI_FEEL, $typeRef : typeRef )
  not( ItemDefinition( name == $typeRef ) )
then
  reporter.report( DMNMessage.Severity.ERROR,  $o , Msg.TYPEREF_NOT_FEEL_NOT_DEF );
end
