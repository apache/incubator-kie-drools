/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */


TestCase "Exists Node unification test case"

import org.drools.core.test.model.Cheese;
import org.drools.core.test.model.Person;

Setup
    ObjectTypeNode:
        otnLeft1, org.drools.core.base.DroolsQueryImpl;
    LeftInputAdapterNode:
        lian0, otnLeft1;
    ObjectTypeNode:
        otnRight1, Person;
    ObjectTypeNode:
        otnRight2, Cheese;
    Binding:
         v, 0, org.drools.core.base.DroolsQueryImpl, [0], java.lang.String;
         $age, 0, org.drools.core.base.DroolsQueryImpl, [1], java.lang.Integer;
         p1, 1, Person, age;
    ExistsNode:
        exists1, lian0, otnRight1;
        name, :=,v;
        age, ==, $age;
    JoinNode:
        join2, exists1, otnRight2;
        type, :=, v;
    Facts:
        new org.drools.core.base.DroolsQueryImpl( "x", [org.kie.api.runtime.rule.Variable.v, 35], null, true),
        new Person('bobba', 36),
        new Person('yoda', 35);

Test "assert right then left then add right block"
    /**
     * Insert Person
     */
    assert:
        otnRight1, [h1];
    /**
     * Insert Query
     */
    assert:
        otnLeft1, [h0];
    exists1:
        rightMemory, [h1];
    exists1:
        leftMemory, [[h0]];
    join2:
        leftMemory, [];
    /**
     * Insert Person (blocker)
     */
    assert:
        otnRight1, [h2];
    exists1:
        rightMemory, [h1];
        rightMemory, [h2]; // h2 is present but in a different bucket        
    exists1:
        leftMemory, [];
    join2:
        leftMemory, [[h0]];       
        
Test "assert right, then right blocker then left"
    /**
     * Insert Person
     */
    assert:
        otnRight1, [h1];
    /**
     * Insert Person (blocker)
     */
    assert:
        otnRight1, [h2];        
    /**
     * Insert Query
     */
    assert:
        otnLeft1, [h0];
    exists1:
        rightMemory, [h1]; 
        rightMemory, [h2]; // h2 is present but in a different bucket
    exists1:
        leftMemory, [];
    join2:
        leftMemory, [[h0]]; 

Test "assert right then left then modify right to block"
    /**
     * Insert Person
     */
    assert:
        otnRight1, [h1];
    /**
     * Insert Query
     */
    assert:
        otnLeft1, [h0];
    exists1:
        rightMemory, [h1];
    exists1:
        leftMemory, [[h0]];
    join2:
        leftMemory, [];
    /**
     * Modify Person (blocker)
     */
    With:
        h1, age = 35;
    modify:
        otnRight1, [h1];
    exists1:
        rightMemory, [h1]; 
    exists1:
        leftMemory, [];
    join2:
        leftMemory, [[h0]];        
        
Test "assert right then left then modify left to block"
    /**
     * Insert Person
     */
    assert:
        otnRight1, [h1];
    /**
     * Insert Query
     */
    assert:
        otnLeft1, [h0];
    exists1:
        rightMemory, [h1];
    exists1:
        leftMemory, [[h0]];
    join2:
        leftMemory, [];
    /**
     * Modify Person (blocker)
     */
    With:
        h0, elements[1] = 36;
    modify:
        otnLeft1, [h0];
    exists1:
        rightMemory, [h1]; 
    exists1:
        leftMemory, [];
    join2:
        leftMemory, [[h0]];         
        
Test "assert left then right then add right block"
    /**
     * Insert Query
     */
    assert:
        otnLeft1, [h0];        
    /**
     * Insert Person
     */
    assert:
        otnRight1, [h1];

    exists1:
        rightMemory, [h1];
    exists1:
        leftMemory, [[h0]];
    join2:
        leftMemory, [];        
    /**
     * Insert Person (blocker)
     */
    assert:
        otnRight1, [h2];
    exists1:
        rightMemory, [h1];
        rightMemory, [h2]; // h2 is present but in a different bucket
    exists1:
        leftMemory, [];
    join2:
        leftMemory, [[h0]];                                     
