/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

{
    // the following would be input parameters,
    // shown here for completeness
    holiday : date("2017-01-01"),

    // Joe is working in Tokyo
    shift start : date and time( "2017-01-02T08:00:00+09:00[Asia/Tokyo]" ),
    shift end : date and time( "2017-01-02T09:30:00+09:00[Asia/Tokyo]" ),

    // holiday in california
    holiday start california: date and time( holiday, time("00:00:00-08:00") ),
    holiday end california: date and time( holiday, time("23:59:59-08:00") ),

    holiday start tokyo: date and time( holiday, time("00:00:00+09:00") ),
    holiday end tokyo: date and time( holiday, time("23:59:59+09:00") ),

    // rate factor if joe is paid based on california's time
    rate factor california : if shift start > holiday start california and
                     shift end < holiday end california
                  then
                     1.5
                  else
                     1.0,

    // rate factor if joe is paid based on tokyo's time
    rate factor tokyo : if shift start > holiday start tokyo and
                     shift end < holiday end tokyo
                  then
                     1.5
                  else
                     1.0,

    // date/time comparison with and without timezones:
    comparison : date and time( holiday, time("00:00:00+00:00")) = date and time( holiday, time("00:00:00")),

    comparison2 : date and time( "2017-01-02T08:00:00+09:00" ) - date and time( "2017-01-01T16:00:00-07:00" ),

    date check : date( date and time( "2017-01-02T08:00:00+09:00" ) ),
    date check2 : date( date and time( "2017-01-01T16:00:00-07:00" ) ),

    date arithmetic 1 : date and time( "2015-01-28T00:00:00" ) + duration( "P0Y1M" ),
    date arithmetic 2 : date and time( "2015-01-29T00:00:00" ) + duration( "P0Y1M" ),
    date arithmetic 3 : date and time( "2015-01-30T00:00:00" ) + duration( "P0Y1M" ),
    date arithmetic 4 : date and time( "2015-01-31T00:00:00" ) + duration( "P0Y1M" ),

    date arithmetic 5 : date and time( "2015-01-28T00:00:00" ) + duration( "P30D" ),
    date arithmetic 6 : date and time( "2015-01-29T00:00:00" ) + duration( "P30D" ),
    date arithmetic 7 : date and time( "2015-01-30T00:00:00" ) + duration( "P30D" ),
    date arithmetic 8 : date and time( "2015-01-31T00:00:00" ) + duration( "P30D" ),

    current : now()
}


