@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand

droolsRepo = 'drools'

pipeline {
    agent {
        label 'kie-rhel7 && kie-mem16g && !built-in'
    }
    tools {
        maven env.BUILD_MAVEN_TOOL
        jdk env.BUILD_JDK_TOOL
    }
    options {
        timestamps()
        timeout(time: 360, unit: 'MINUTES')
    }
    environment {
        // QUARKUS_BRANCH should be defined directly into the job environment

        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")
        MAVEN_OPTS = '-Xms1024m -Xmx6g -XX:PermSize=128m -XX:MaxPermSize=512m'
    }
    stages {
        stage('Initialize') {
            steps {
                script {
                    checkoutDroolsRepo()
                }
            }
        }

        stage('Setup integration branch') {
            when {
                expression { useIntegrationBranch() }
            }
            steps {
                script {
                    // Calculate integration branch
                    setIntegrationBranch("${getKogitoBranch()}-integration-quarkus-${getQuarkusBranch()}")
                    mergeOnIntegrationBranch(droolsRepo, getDroolsBranch(), true)
                }
            }
        }

        stage('Build quarkus') {
            when {
                expression { getQuarkusBranch() }
            }
            steps {
                script {
                    checkoutQuarkusRepo()
                    getBasicMavenCommand('quarkus')
                        .withProperty('quickly')
                        .run('clean install')

                    // Update downstream repositories dependencies
                    mavenCompareQuarkusDependencies(droolsRepo, 'drools-build-parent')
                    mavenSetProperty(droolsRepo, 'version.io.quarkus', '999-SNAPSHOT')
                }
            }
            post {
                always {
                    script {
                        cleanContainers()
                    }
                }
            }
        }

        stage('Build Drools') {
            steps {
                script {
                    getFullMavenCommand(droolsRepo)
                        .withProperty('maven.test.failure.ignore', true)
                        .run('clean install')
                }
            }
            post {
                always {
                    script {
                        cleanContainers()
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            sendNotification()
        }
        always {
            script {
                junit testResults: '**/target/surefire-reports/**/*.xml, **/target/failsafe-reports/**/*.xml', allowEmptyResults: true
                util.archiveConsoleLog()
            }
        }
        cleanup {
            script {
                util.cleanNode('docker')
            }
        }
    }
}

void sendNotification() {
    String additionalInfo = ''
    if(useIntegrationBranch()) {
        additionalInfo += """
If any changes need to be done, please perform them on branch **${getIntegrationBranch()}**
"""
    }
    additionalInfo = """
${getReproducer()}
"""
    mailer.sendMarkdownTestSummaryNotification("${NOTIFICATION_JOB_NAME}${useIntegrationBranch() ? ' - Integration' : ''}", "[${getDroolsBranch()}] Drools", [env.KOGITO_CI_EMAIL_TO], additionalInfo)
}

void checkoutDroolsRepo() {
    dir(droolsRepo) {
        deleteDir()
        checkout(githubscm.resolveRepository(droolsRepo, params.GIT_AUTHOR, getDroolsBranch(), false))
    }
}

void checkoutQuarkusRepo() {
    dir('quarkus') {
        deleteDir()
        checkout(githubscm.resolveRepository('quarkus', 'quarkusio', getQuarkusBranch(), false))
    }
}

void mergeOnIntegrationBranch(String repoName, String baseBranch, boolean pushChanges = false) {
    echo "Merge changes for repository ${repoName} from ${baseBranch} to ${getIntegrationBranch()}"
    dir(repoName) {
        getOrCreateGitBranch(getIntegrationBranch())
        sh "git merge origin/${baseBranch} --no-ff"
        if(pushChanges){
            githubscm.pushObject('origin', getIntegrationBranch(),  params.GIT_AUTHOR_CREDS_ID)
        }
    }
}

MavenCommand getBasicMavenCommand(String directory, boolean includeSettings = true) {
    def mvnCmd = new MavenCommand(this, ['-fae', '-ntp'])
                .inDirectory(directory)
    if (includeSettings) {
        mvnCmd.withSettingsXmlId('kogito_release_settings')
    }
    return mvnCmd
}

MavenCommand getEcosystemMavenCommand(String directory, boolean includeSettings = true) {
    def mvnCmd = getBasicMavenCommand(directory, includeSettings)

    if (env.BUILD_MVN_OPTS) {
        mvnCmd.withOptions([ env.BUILD_MVN_OPTS ])
    }

    return mvnCmd
}

MavenCommand getFullMavenCommand(String directory, boolean includeSettings = true) {
    def mvnCmd = getEcosystemMavenCommand(directory, includeSettings)
    if (isNative()) {
        mvnCmd.withProfiles(['native'])
                .withProperty('quarkus.native.container-build', true)
                .withProperty('quarkus.native.container-runtime', 'docker')
                .withProperty('quarkus.profile', 'native') // Added due to https://github.com/quarkusio/quarkus/issues/13341

        String builderImage = getNativeBuilderImage()
        if (builderImage) {
            mvnCmd.withProperty('quarkus.native.builder-image', builderImage)
        }
    }
    return mvnCmd
}

void mavenCompareQuarkusDependencies(String repo, String artifactId) {
    maven.mvnCompareDependencies(getBasicMavenCommand(repo), 'io.quarkus:quarkus-bom:999-SNAPSHOT', ":${artifactId}", true, true)
}

void mavenSetProperty(String repo, String property, String newVersion) {
    maven.mvnSetVersionProperty(getBasicMavenCommand(repo), property, newVersion)
}

void cleanContainers() {
    cloud.cleanContainersAndImages('docker')
}

boolean isNative() {
    return env.NATIVE ? env.NATIVE.toBoolean() : false
}

String getNativeBuilderImage() {
    return env.NATIVE_BUILDER_IMAGE
}

String getQuarkusBranch() {
    return env['QUARKUS_BRANCH'] ?: ''
}

String getKogitoBranch() {
    return util.calculateTargetReleaseBranch(getDroolsBranch(), -7)
}

String getDroolsBranch() {
    return params.BUILD_BRANCH_NAME
}

boolean useIntegrationBranch() {
    return env.USE_INTEGRATION_BRANCH ? env.USE_INTEGRATION_BRANCH.toBoolean() : false
}

void setIntegrationBranch(String branch) {
    echo "Set integration branch ${getIntegrationBranch()}"
    env.INTEGRATION_BRANCH = branch
}

String getIntegrationBranch() {
    return env.INTEGRATION_BRANCH
}

void getOrCreateGitBranch(String branch) {
    sh 'git fetch origin'
    String branchRemoteResult = sh(script: "git ls-remote origin ${branch} | wc -l", returnStdout: true).trim()
    if (Integer.parseInt(branchRemoteResult) > 0) {
        echo "Branch ${branch} already exist ... will not create it. Checking out !"
        sh "git checkout ${branch}"
    } else {

        echo "Branch ${branch} does not exist ... gonna create it"
        githubscm.createBranch(branch)
    }
}

String getReproducer() {
    String droolsBranch = useIntegrationBranch() ? getIntegrationBranch() : getDroolsBranch()
    String reproducer = """
```spoiler Commands to reproduce
git clone --branch ${droolsBranch} --depth 1 https://github.com/kiegroup/drools.git
"""
    if (getQuarkusBranch()) {
        reproducer += """
git clone --branch ${getQuarkusBranch()} --depth 1 https://github.com/quarkusio/quarkus.git
cd quarkus && ${getBasicMavenCommand('quarkus', false).withProperty('quickly').getFullRunCommand('clean install')}
cd ../drools
mvn -pl :drools-build-parent versions:compare-dependencies -DremotePom=io.quarkus:quarkus-bom:999-SNAPSHOT -DupdatePropertyVersions=true -DupdateDependencies=true -DgenerateBackupPoms=false
mvn -e versions:set-property -Dproperty=version.io.quarkus -DnewVersion=999-SNAPSHOT -DallowSnapshots=true -DgenerateBackupPoms=false
cd ..
"""
    }
    reproducer += """
cd drools && ${getFullMavenCommand(droolsRepo, false).withProperty('maven.test.failure.ignore', true).getFullRunCommand('clean install')}
"""
    reproducer += '```'
    return reproducer
}
