#created on: 9.12.2007
package org.drools.verifier.redundancy

#list any import classes here.
global VerifierReport result;



#declare any global variables here
import java.util.Collection;
import java.util.ArrayList;

import org.drools.verifier.components.SubRule;
import org.drools.verifier.data.VerifierReport;
import org.drools.verifier.report.components.Redundancy;
import org.drools.verifier.report.components.RedundancyType;
import org.drools.verifier.report.components.VerifierMessage;
import org.drools.verifier.report.components.Cause;
import org.drools.verifier.report.components.CauseType;
import org.drools.verifier.report.components.Severity;
import org.drools.verifier.report.components.MessageType;

#
# Dependencies:
#
#	Needs at least one rule from these packages:
#		package org.drools.verifier.equivalency.rules
#			rule "Find equivalent Rules"
#		package org.drools.verifier.redundancy.rules
#			rule "Find redundant Rules, strong redundancy"
#
#	And this rule:
#		package org.drools.verifier.redundancy.possibilities
#			rule "Find rule possibility redundancy"
#
rule "Find redundant rule possibilities from different rules"
	salience -100
	when
		$ruleRedundancy :Redundancy(
			left.causeType == CauseType.RULE
		)

		$rulePossibilityRedundancy :Redundancy(
			left.causeType == CauseType.RULE_POSSIBILITY
		)

		# Check that redundant possibilities are from redundant rules
		exists SubRule(
			this == $rulePossibilityRedundancy.left,
			eval( ruleGuid.equals( $ruleRedundancy.getLeft().getGuid() ) )
		)

		exists SubRule(
			this == $rulePossibilityRedundancy.right,
			eval( ruleGuid.equals( $ruleRedundancy.getRight().getGuid() ) )
		)

		# Check that the possibilties are redundant to each other.
		exists Redundancy(
			left.causeType == CauseType.RULE_POSSIBILITY,
			left == $rulePossibilityRedundancy.right,
			right == $rulePossibilityRedundancy.left
		)

	then
		Collection causes = new ArrayList();
		causes.add( $rulePossibilityRedundancy.getLeft() );
		causes.add( $rulePossibilityRedundancy.getRight() );

		String message;
		if( $ruleRedundancy.getType() == RedundancyType.WEAK ) {
			message = "Rules are equivalent.";
		} else {
			message = "Rules are redundant.";
		}

		result.add(new VerifierMessage(
						Severity.WARNING, MessageType.REDUNDANCY,
						$rulePossibilityRedundancy, message, causes ) );
end

#
# Dependencies:
#
#	Needs at least one rule from these packages:
#		package org.drools.verifier.equivalency.rules
#			rule "Find equivalent Rules"
#		package org.drools.verifier.redundancy.rules
#			rule "Find redundant Rules, strong redundancy"
#
#	And this rule:
#		package org.drools.verifier.redundancy.possibilities
#			rule "Find rule possibility redundancy"
#
rule "Find subsumptant rule possibilities from different rules"
	salience -100
	when
		$ruleRedundancy :Redundancy(
			left.causeType == CauseType.RULE
		)

		$rulePossibilityRedundancy :Redundancy(
			left.causeType == CauseType.RULE_POSSIBILITY
		)

		# Check that redundant possibilities are from redundant rules
		exists SubRule(
			this == $rulePossibilityRedundancy.left,
			eval( ruleGuid.equals( $ruleRedundancy.getLeft().getGuid() ) )
		)

		exists SubRule(
			this == $rulePossibilityRedundancy.right,
			eval( ruleGuid.equals( $ruleRedundancy.getRight().getGuid() ) )
		)

		# Check that the left side is redundant to right, but the right side is not redundant to left.
		not Redundancy(
			left.causeType == CauseType.RULE_POSSIBILITY,
			left == $rulePossibilityRedundancy.right,
			right == $rulePossibilityRedundancy.left
		)

	then
		Collection causes = new ArrayList();
		causes.add( $rulePossibilityRedundancy.getLeft() );
		causes.add( $rulePossibilityRedundancy.getRight() );

		result.add(new VerifierMessage(
						Severity.WARNING, MessageType.REDUNDANCY,
						$rulePossibilityRedundancy, "Rules are subsumptant.", causes ) );
end
