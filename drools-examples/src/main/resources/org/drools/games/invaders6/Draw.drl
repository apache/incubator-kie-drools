/*
 * Copyright 2015 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package org.drools.games.invaders6;

import org.drools.games.KeyPressed
import org.drools.games.Run
import org.drools.games.invaders.Ship
import org.drools.games.invaders.Bullet
import org.drools.games.invaders.Invader
import org.drools.games.GameUI
import org.drools.games.invaders.InvadersConfiguration

global GameUI ui
global InvadersConfiguration conf


import javax.imageio.ImageIO
import java.awt.Color;
import java.awt.Graphics

dialect "mvel"

rule DrawShip agenda-group "Draw" when
  s : Ship()
      Run()
then
   g = ui.getGraphics();
   g.setColor( Color.BLACK ); // background
   g.fillRect( s.x - s.dx - s.dy, s.y, s.width, s.height ); // restore the previous blackground
   g.drawImage( ImageIO.read( GameUI.class.getResource( "invaders/ship.gif" ) ), s.x, s.y, s.width, s.height, ui.getCanvas() );
end

rule DrawLiveInvader agenda-group "Draw" when
  i : Invader( alive == true)
      Run()
then
   g = ui.getGraphics();
   g.setColor( Color.BLACK ); // background
   g.fillRect( i.x - i.dx, i.y - i.dy, i.width, i.height ); // restore the previous blackground
   g.drawImage( ImageIO.read( GameUI.class.getResource( "invaders/invader1.gif" ) ), i.x, i.y, i.width, i.height, ui.getCanvas() );
end

rule DrawDeadInvader agenda-group "Draw" when
  i : Invader( alive == false)
      Run()
then
   g = ui.getGraphics();
   g.setColor( Color.BLACK ); // background
   g.fillRect( i.x, i.y, i.width, i.height ); // restore the previous blackground
end

rule DrawBullet agenda-group "Draw" when
  b : Bullet()
      Run()
then
   g = ui.getGraphics();
   g.setColor( Color.BLACK ); // background
   g.fillRect( b.x - b.dx , b.y - b.dy , b.width, b.height ); // restore the previous blackground

   g.setColor( Color.GREEN ); // bullet
   g.fillRect( b.x , b.y, b.width, b.height );
end

rule DrawBulletOffscreen agenda-group "Draw" when
    b : Bullet( y <= 0 )
    Run()
then
   g = ui.getGraphics();
   g.setColor( Color.BLACK ); // background
   g.fillRect( b.x , b.y, b.width, b.height ); // restore the background
   retract( b )
end

rule Repaint agenda-group "Draw"  salience -100 when
    Run()
then
   ui.repaint();
end
