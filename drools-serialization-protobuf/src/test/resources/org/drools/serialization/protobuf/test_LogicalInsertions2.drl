/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

package org.drools.compiler.test;

import org.drools.mvel.compiler.Alarm;
import org.drools.mvel.compiler.EmergencyTeam;
import org.drools.mvel.compiler.Sensor;

global java.util.List events;

rule "Everything is Normal"
    when
        not Alarm( )
    then
        events.add("Everything is Normal");
end   

rule "Problem detected" 
    salience 10
    when
        sensor : ( Sensor( temperature > 100 ) or Sensor( pressure > 150 ) )
    then
        events.add( "Problem detected: "+sensor.toString() );
        insertLogical( new Alarm( "Call emergency team" ) );
end 

rule "Call Emergency Team"
    when
        Alarm ( )
    then
        events.add( "Calling Emergency Team" );
        insertLogical( new EmergencyTeam() );
end

rule "Emergency Team solves temperature" 
    salience 20
    when 
        EmergencyTeam()
        sensor : Sensor( temperature > 100 )
    then
        events.add( "Solving temperature problem" );
        sensor.setTemperature(80);
        update(sensor);
end

rule "Emergency Team solves pressure" 
    salience 20
    when 
        EmergencyTeam()
        sensor : Sensor( pressure > 150 )
    then
        events.add( "Solving pressure problem" );
        sensor.setPressure(80);
        update(sensor);
end
