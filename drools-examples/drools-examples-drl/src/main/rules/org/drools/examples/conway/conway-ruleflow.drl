package org.drools.examples
 
import org.drools.examples.conway.Cell;
import org.drools.examples.conway.CellGrid;
import org.drools.examples.conway.Neighbor;
import org.drools.examples.conway.Phase;
import org.drools.examples.conway.CellState;

import org.drools.WorkingMemory;
import org.drools.common.InternalWorkingMemoryActions;
import org.drools.RuleBase;


rule "register north east"
	ruleflow-group "register neighbor"
when
    CellGrid( $numberOfColumns : numberOfColumns )
	$cell: Cell( $row : row > 0, $col : col < ( $numberOfColumns - 1 ) )			
	$northEast : Cell( row  == ($row - 1), col == ( $col + 1 ) )	
then					
	insert( new Neighbor( $cell, $northEast ) );
	insert( new Neighbor( $northEast, $cell ) );		
end

rule "register north"
	ruleflow-group "register neighbor"	
when
	$cell: Cell( $row : row > 0, $col : col )	
	$north : Cell( row  == ($row - 1), col == $col )	
then		
	insert( new Neighbor( $cell, $north ) );
	insert( new Neighbor( $north, $cell ) );		
end

rule "register north west"
	ruleflow-group "register neighbor"
when
	$cell: Cell( $row : row > 0, $col : col > 0 )			
	$northWest : Cell( row  == ($row - 1), col == ( $col - 1 ) )						
then		
	insert( new Neighbor( $cell, $northWest ) );
	insert( new Neighbor( $northWest, $cell ) );		
end

rule "register west"
	ruleflow-group "register neighbor"
when
	$cell: Cell( $row : row >= 0, $col : col > 0 )			
	$west : Cell( row  == $row, col == ( $col - 1 ) )						
then		
	insert( new Neighbor( $cell, $west ) );
	insert( new Neighbor( $west, $cell ) );			
end

rule "Kill The Lonely"
	ruleflow-group "evaluate"
	no-loop
when
#	A live cell has fewer than 2 live neighbors
	theCell: Cell(liveNeighbors < 2, cellState == CellState.LIVE, phase == Phase.EVALUATE)
then
	theCell.setPhase(Phase.KILL);
	update( theCell );
end

rule "Kill The Overcrowded"
	ruleflow-group "evaluate"
	no-loop
when
#	A live cell has more than 3 live neighbors
	theCell: Cell(liveNeighbors > 3, cellState == CellState.LIVE, phase == Phase.EVALUATE)
then
	theCell.setPhase(Phase.KILL);
	update( theCell );
end

rule "Give Birth"
	ruleflow-group "evaluate"
	no-loop
when
#	A dead cell has 3 live neighbors
	theCell: Cell(liveNeighbors == 3, cellState == CellState.DEAD, phase == Phase.EVALUATE)
then
	theCell.setPhase(Phase.BIRTH);
	update( theCell );
end

rule "reset calculate"
	ruleflow-group "reset calculate"
when
then
	WorkingMemory wm = drools.getWorkingMemory();
	wm.clearRuleFlowGroup( "calculate" );
end

rule "kill"
	ruleflow-group "kill"
	no-loop
when
	theCell: Cell(phase == Phase.KILL)
then
	theCell.setCellState(CellState.DEAD);
	theCell.setPhase(Phase.DONE);	
	update( theCell );
end	
 
rule "birth"
	ruleflow-group "birth"
	no-loop
when
	theCell: Cell(phase == Phase.BIRTH)
then
	theCell.setCellState(CellState.LIVE);
	theCell.setPhase(Phase.DONE);
	update( theCell );	
end	

rule "Calculate Live"
	ruleflow-group "calculate"
	lock-on-active	
when
	theCell: Cell(cellState == CellState.LIVE)
	Neighbor(cell == theCell, $neighbor : neighbor)	
then
	$neighbor.setLiveNeighbors( $neighbor.getLiveNeighbors() + 1 );
	$neighbor.setPhase( Phase.EVALUATE );	
	update( $neighbor );
end	

rule "Calculate Dead"
	ruleflow-group "calculate"
	lock-on-active	
when
	theCell: Cell(cellState == CellState.DEAD)
    Neighbor(cell == theCell, $neighbor : neighbor )
then
	$neighbor.setLiveNeighbors( $neighbor.getLiveNeighbors() - 1 );
	$neighbor.setPhase( Phase.EVALUATE );
	update( $neighbor );	
end	

rule "Kill All"
	ruleflow-group "kill all"	
	no-loop
when
	theCell: Cell(cellState == CellState.LIVE)
then
	theCell.setCellState(CellState.DEAD);
	update( theCell );
end